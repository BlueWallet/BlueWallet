import org.apache.tools.ant.taskdefs.condition.Os

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "31.0.0"
        minSdkVersion = 28
        compileSdkVersion = 31
        targetSdkVersion = 31
        googlePlayServicesVersion = "16.+"
        googlePlayServicesIidVersion = "16.0.1"
        firebaseVersion = "17.3.4"
        firebaseMessagingVersion = "20.2.1"

        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            // For Android Users, we need to use NDK 23, otherwise the build will
            // fail due to paths longer than the OS limit
            ndkVersion = "23.1.7779620"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.bugsnag:bugsnag-android-gradle-plugin:5.+")
        classpath 'com.google.gms:google-services:4.3.10'  // Google Services plugin
        classpath("com.android.tools.build:gradle:7.0.4")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:4.1.2")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter() {
            content {
                includeModule("com.facebook.yoga", "proguard-annotations")
                includeModule("com.facebook.fbjni", "fbjni-java-only")
                includeModule("com.facebook.fresco", "fresco")
                includeModule("com.facebook.fresco", "stetho")
                includeModule("com.facebook.fresco", "fbcore")
                includeModule("com.facebook.fresco", "drawee")
                includeModule("com.facebook.fresco", "imagepipeline")
                includeModule("com.facebook.fresco", "imagepipeline-native")
                includeModule("com.facebook.fresco", "memory-type-native")
                includeModule("com.facebook.fresco", "memory-type-java")
                includeModule("com.facebook.fresco", "nativeimagefilters")
                includeModule("com.facebook.stetho", "stetho")
                includeModule("com.wei.android.lib", "fingerprintidentify")
            }
        }
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
            // All of Detox' artifacts are provided via the npm module
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 31
                buildToolsVersion '31.0.0'
                 defaultConfig {
                    minSdkVersion 28
                }
            }
        }
    }
}